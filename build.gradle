buildscript {
    ext {
        kotlinVersion = '1.3.41'
        springBootVersion = '2.1.6.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects{
    defaultTasks 'clean', 'build'
}

subprojects { projects ->

    apply from: "$rootDir/dependencies.gradle"
    apply plugin: 'kotlin'

    group = 'org.suggs.sandbox.eventsourcing'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        implementation libs.kotlin,
                libs.kotlinReflect,
                libs.slf4j

        testImplementation libs.test.junitApi,
                libs.test.assertj,
                libs.test.mockito

        testRuntimeOnly libs.test.junitEngine,
                libs.logback
    }
    
    ext {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

}
